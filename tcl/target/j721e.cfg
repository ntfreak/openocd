#
# Texas Instruments J721E: http://www.ti.com/lit/pdf/spruil1
#

if { [info exists CHIPNAME] } {
	set _CHIPNAME $CHIPNAME
} else {
	set _CHIPNAME j721e
}

if { [info exists J721E_JTAG_TAPID] } {
	set _J721E_DAP_TAPID $J721E_JTAG_TAPID
} else {
	set _J721E_DAP_TAPID 0x0bb6402f
}

if { [info exists A72CHIPCORES] } {
    set _a72_cores $A72CHIPCORES
} else {
    set _a72_cores 2
}

if { [info exists R5CHIPCORES] } {
    set _r5_cores $R5CHIPCORES
} else {
    set _r5_cores 6
}

jtag newtap $_CHIPNAME cpu -irlen 4 -expected-id $_J721E_DAP_TAPID
dap create $_CHIPNAME.dap -chain-position $_CHIPNAME.cpu

set _TARGETNAME $_CHIPNAME.cpu

set _CTINAME $_CHIPNAME.cti

# CM3 the very first processor.
set CM3_DWTBASE		{0xE0001000}
set CM3_CTIBASE		{0x3C016000}
set CM3_ETBBASE		{0x3C025000}
set CM3_TPIUBASE	{0x20004000}


# M3 is always active, so keep it alive
cti create $_CTINAME.m3 -dap $_CHIPNAME.dap -ap-num 7 -ctibase [lindex $CM3_CTIBASE 0]
set _command "target create $_TARGETNAME.m3 cortex_m -dap $_CHIPNAME.dap -ap-num 7"
set _command "$_command -defer-examine"
eval $_command
$_TARGETNAME.m3 configure -event reset-assert { }

proc m3_up { args } {
	global _TARGETNAME
	global _CHIPNAME

	# To access M3, we need to enable the JTAG access for the same.
	# Ensure Power-AP unlocked
	$_CHIPNAME.dap apreg 3 0xf0 0x00190000
	$_CHIPNAME.dap apreg 3 0x44 0x00102098

        $_TARGETNAME.m3 arp_examine
}

# All the A72s are the next processors.
set A72_DBGBASE {0x90410000 0x90510000}
set A72_CTIBASE {0x90420000 0x90520000}
set A72_PMUBASE {0x90430000 0x90530000}
set A72_ETMBASE {0x90440000 0x90540000}

for { set _core 0 } { $_core < $_a72_cores } { incr _core } {

    cti create $_CTINAME.a72.$_core -dap $_CHIPNAME.dap -ap-num 1 \
        -ctibase [lindex $A72_CTIBASE $_core]

    set _command "target create $_TARGETNAME.a72.$_core aarch64 -dap $_CHIPNAME.dap \
        -dbgbase [lindex $A72_DBGBASE $_core] -cti $_CTINAME.a72.$_core"

    # On J721E, A72s do not come up by default. examination will fail
    set _command "$_command -defer-examine"
    if { $_core != 0 } {
        set _smp_command "$_smp_command $_TARGETNAME.a72.$_core"
    } else {
        set _smp_command "target smp $_TARGETNAME.a72.$_core"
    }

    eval $_command
}

proc a72_up { args } {
    global _TARGETNAME
    foreach { _core } [set args] {
        $_TARGETNAME.a72.$_core arp_examine
        $_TARGETNAME.a72.$_core aarch64 dbginit
    }
}

proc a72_smp { args } {
    global _TARGETNAME
    global _a72_cores
    for { set _core 0 } { $_core < $_a72_cores } { incr _core } {
        $_TARGETNAME.a72.$_core arp_examine
        $_TARGETNAME.a72.$_core aarch64 dbginit
        $_TARGETNAME.a72.$_core aarch64 smp on
    }
    aarch64 smp on
}

# And we add up the R5s
#	        (0)MCU 0   (1)MCU 1   (2)MAIN_0_0 (3)MAIN_0_1 (4)MAIN_1_0 (5)MAIN_1_1
set R5_DBGBASE {0x9d010000 0x9d012000 0x9d410000 0x9d412000 0x9d510000 0x9d512000}
set R5_CTIBASE {0x9d018000 0x9d019000 0x9d418000 0x9d419000 0x9d518000 0x9d519000}
set R5_ETMBASE {0x9d01c000 0x9d01d000 0x9d41c000 0x9d41d000 0x9d51c000 0x9d51d000}

for { set _core 0 } { $_core < $_r5_cores } { incr _core } {
    cti create $_CTINAME.r5.$_core -dap $_CHIPNAME.dap -ap-num 1 \
        -ctibase [lindex $R5_CTIBASE $_core]
    set _command "target create $_TARGETNAME.r5.$_core cortex_r4 -dap $_CHIPNAME.dap \
        -dbgbase [lindex $R5_DBGBASE $_core] -ap-num 1"
        # non-boot core examination may fail - wait till startup of additional core
    set _command "$_command -defer-examine"

    eval $_command
}

# Lockstep processors will NOT have CPU1 active
proc mcu_r5_up { args } {
    global _TARGETNAME
    set _base_core_id 0
    foreach { _core } [set args] {
	set _core [expr {$_core + $_base_core_id}]
        $_TARGETNAME.r5.$_core arp_examine
        $_TARGETNAME.r5.$_core cortex_r4 dbginit
    }
}

proc main0_r5_up { args } {
    global _TARGETNAME
    set _base_core_id 2
    foreach { _core } [set args] {
	set _core [expr {$_core + $_base_core_id}]
        $_TARGETNAME.r5.$_core arp_examine
        $_TARGETNAME.r5.$_core cortex_r4 dbginit
    }
}

proc main1_r5_up { args } {
    global _TARGETNAME
    set _base_core_id 4
    foreach { _core } [set args] {
	set _core [expr {$_core + $_base_core_id}]
        $_TARGETNAME.r5.$_core arp_examine
        $_TARGETNAME.r5.$_core cortex_r4 dbginit
    }
}
