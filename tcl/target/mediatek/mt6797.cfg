# MediaTek MT6797 (Helio X20)
#
# This SoC has three main ARMv8 clusters: A 1.4 GHz Cortex-A53 cluster,
# a 2.0 GHz Cortex-A53 cluster, and a 2.5 GHz Cortex-A72 cluster. These
# clusters are in a big.LITTLE configuration for optimum performance and
# power consumption.
#
# In addition to the ARMv8 clusters, this SoC also has a Cortex-M4
# (typically used as a "sensor hub") and a Cortex-A9 (whose purpose is
# currently unknown).

source [find target/swj-dp.tcl]

set _CHIPNAME mt6797

#
# Main DAP
#
if { [info exists DAP_TAPID] } {
    set _DAP_TAPID $DAP_TAPID
} else {
    set _DAP_TAPID 0x4ba00477
}

# declare the one JTAG/SWD tap to access the DAP
swj_newdap $_CHIPNAME tap -irlen 4 -ircapture 0x1 -irmask 0xf -expected-id $_DAP_TAPID -ignore-version -enable
dap create $_CHIPNAME.dap -chain-position $_CHIPNAME.tap

set soc_core 0

proc declare_cluster {dapname targetname ap ctis} {
    global soc_core

    set _smp_command ""

    for { set _core 0 } { $_core < [llength $ctis] } { incr _core 1 } {

        cti create ${targetname}.cti$_core -dap $dapname \
            -ctibase [lindex $ctis $_core] -ap-num $ap

        set _command "target create ${targetname}.cpu$_core aarch64 \
                             -dap $dapname -coreid $soc_core \
                             -cti ${targetname}.cti$_core \
                             -defer-examine"

        if { $_core == 0 } {
            # uncomment when "hawt" rtos is merged
            # set _command "$_command -rtos hawt"
            set _smp_command "target smp ${targetname}.cpu$_core"
        } else {
            set _smp_command "$_smp_command ${targetname}.cpu$_core"
        }

        eval $_command

        incr soc_core
    }

    eval $_smp_command
}

# declare the 4 Cortex-A53 cores in the 1.4 GHz cluster
declare_cluster $_CHIPNAME.dap $_CHIPNAME.a53.0 1 {0x80420000 0x80520000 0x80620000 0x80720000}

# declare the 4 Cortex-A53 cores in the 2.0 GHz cluster
declare_cluster $_CHIPNAME.dap $_CHIPNAME.a53.1 1 {0x80820000 0x80920000 0x80a20000 0x80b20000}

# declare the 2 Cortex-A72 cores in the 2.5 GHz cluster
declare_cluster $_CHIPNAME.dap $_CHIPNAME.a72 1 {0x80c20000 0x80d20000}

# declare the Cortex-A9
set _TARGETNAME $_CHIPNAME.a9.cpu
target create ${_TARGETNAME} cortex_a -dap $_CHIPNAME.dap -defer-examine

# declare the Cortex-M4 sensor hub
set _TARGETNAME $_CHIPNAME.m4.cpu
target create ${_TARGETNAME} cortex_m -dap $_CHIPNAME.dap -defer-examine
