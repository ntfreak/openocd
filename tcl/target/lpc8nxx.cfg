# NXP LPC8Nxx NHS31xx Cortex-M0+ with 8kB SRAM
# SWD only transport
# Based on NXP proposal https://community.nxp.com/message/1011149
# Many thanks to Dries Moors from NXP support and Tomas Vanek from OpenOCD.

source [find target/swj-dp.tcl]
source [find mem_helper.tcl]

if { [info exists CHIPNAME] } {
	set  _CHIPNAME $CHIPNAME
} else {
	set  _CHIPNAME lpc8nxx
}
swj_newdap $_CHIPNAME cpu -irlen 4 -expected-id 0
dap create $_CHIPNAME.dap -chain-position $_CHIPNAME.cpu
set _TARGETNAME $_CHIPNAME.cpu
target create $_TARGETNAME cortex_m -endian little -dap $_CHIPNAME.dap
$_TARGETNAME configure -work-area-phys 0x10000000 -work-area-size 0x1ff0 -work-area-backup 0
flash bank $_CHIPNAME.flash lpc2000 0x0 0x7800 0 0 $_TARGETNAME lpc800 500

echo "*********************************************************************************"
echo "*         !!!!! IMPORTANT NOTICE FOR LPC8Nxx and NHS31xx CHIPS !!!!!"
echo "* When this IC is in Power-off or Deep Power down mode, the SWD HW block is also"
echo "* unpowered. These modes can be entered by firmware. The default firmware image"
echo "* (flashed in production) makes use of this. Best is to avoid these power modes"
echo "* during development, and only later add them when the functionality is complete."
echo "* Hardware reset is the only way to connect in case the SWD is powered off."
echo "* It's possible to do this automatically with OpenOCD if you wire adapter SRST"
echo "* signal to the chip RESETN pin and add the following in your configuration file:"
echo "*     reset_config srst_only"
echo "*********************************************************************************"

# NXP support kindly provides important information regarding NHS31xx (and LPC8Nxx):
# - VECTRESET will do nothing: VECTRESET is not supported on Cortex-M0, Cortex-M0+,
#   Cortex-M1, and ARMv8-M cores.
# - Implementation of SYSRESETREQ is IC dependent. for the NHS31xx ICs,
#   it will only trigger a reset of the ARM processor, just like can be done with the
#   AIRCR register. The system clock speed (a.o.) is not affected by this.
# OpenOCD should not use the VECTRESET as a default on CM0/0+/1 but it does.
#
if {![using_hla]} {
    # if srst is not fitted use SYSRESETREQ to
    # perform a soft reset
    cortex_m reset_config sysresetreq
}

# Using soft-reset 'reset_config none' is strongly discouraged.
# RESETN sets the system clock to 500 kHz. Unlike soft-reset does not.
# Set the system clock to 500 kHz before reset to simulate the functionality of hw reset.
#
$_TARGETNAME configure -event reset-start "$_TARGETNAME arp_halt; set_sysclk_500khz"
proc set_sysclk_500khz {} {
	echo "Notice: set sysclock to 500kHz."
	set SYSCLKCTRL 0x40048020
	set SYSCLKUEN 0x40048024
	mww $SYSCLKUEN 0
	mmw $SYSCLKCTRL 0x8 0xe
	mww $SYSCLKUEN 1
}

# Do not remap the ARM interrupt vectors to anything but the begining ot the flash.
# Table System memory remap register (SYSMEMREMAP, address 0x4004 8000) bit description
# Bit Symbol  Value   Description
# 0   map         -   interrupt vector remap. 0 after boot.
#                 0   interrupt vector reside in Flash
#                 1   interrupt vector reside in SRAM
# 5:1 offset      -   system memory remap offset. 00000b after boot.
#            00000b   interrupt vectors in flash or remapped to SRAM but no offset
#            00001b -
#            00111b   interrupt vectors offset in flash or SRAM to 1K word segment
#            01000b -
#            11111b   interrupt vectors offset in flash to 1K word segment 8 to 31
# 31:6                reserved
#
$_TARGETNAME configure -event reset-init {
	mww 0x40048000 0x00
}
