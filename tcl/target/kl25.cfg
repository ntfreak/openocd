#
# Freescale Kinetis KL25 devices
#

#
# KL25 devices support both JTAG and SWD transports.
#
source [find target/swj-dp.tcl]

if { [info exists CHIPNAME] } {
    set _CHIPNAME $CHIPNAME
} else {
    set _CHIPNAME kl25
}

if { [info exists ENDIAN] } {
    set _ENDIAN $ENDIAN
} else {
    set _ENDIAN little
}

# Work-area is a space in RAM used for flash programming
# By default use 4kB
if { [info exists WORKAREASIZE] } {
   set _WORKAREASIZE $WORKAREASIZE
} else {
   set _WORKAREASIZE 0x1000
}

if { [info exists CPUTAPID] } {
    set _CPUTAPID $CPUTAPID
} else {
    set _CPUTAPID 0x0bc11477
}

swj_newdap $_CHIPNAME cpu -irlen 4 -expected-id $_CPUTAPID

set _TARGETNAME $_CHIPNAME.cpu
target create $_TARGETNAME cortex_m -endian $_ENDIAN -chain-position $_CHIPNAME.cpu

# It is important that "kinetis mdm check_security" is called for
# 'examine-end' event and not 'eximine-start'. Calling it in 'examine-start'
# causes "kinetis mdm check_security" to fail the first time openocd
# calls it when it tries to connect after the CPU has been power-cycled.
$_CHIPNAME.cpu configure -event examine-end {
	kinetis mdm check_security
}

$_TARGETNAME configure -work-area-phys 0x20000000 -work-area-size $_WORKAREASIZE -work-area-backup 0

set _FLASHNAME $_CHIPNAME.flash
flash bank $_FLASHNAME kinetis 0 0 0 0 $_TARGETNAME

if {![using_hla]} {
   # if srst is not fitted use SYSRESETREQ to
   # perform a soft reset
   cortex_m reset_config sysresetreq
}

proc kl25z_enable_pll {} {
	echo "KL25Z: Enabling PLL"
	# SIM->CLKDIV1 = (uint32_t)0x00020000UL; /* Update system prescalers */
	mww     0x40048044      0x00020000
	# /* Switch to FEI Mode */
	# MCG->C1 = (uint8_t)0x06U;
	mwb 0x40064000  0x06
	# MCG->C2 = (uint8_t)0x00U;
	mwb 0x40064001  0x00
	# /* MCG->C4: DMX32=0,DRST_DRS=1 */
	# MCG->C4 = (uint8_t)((MCG->C4 & (uint8_t)~(uint8_t)0xC0U) | (uint8_t)0x20U);
	mwb 0x40064003  0x37
	#OSC0->CR = (uint8_t)0x80U;
	mwb 0x40065000  0x80
	# MCG->C5 = (uint8_t)0x00U;
	mwb 0x40064004  0x00
	# MCG->C6 = (uint8_t)0x00U;
	mwb 0x40064005  0x00
	sleep 100
}

$_TARGETNAME configure -event reset-init {
	kl25z_enable_pll
}
